#compdef birthday

_birthday () {
    _arguments '1: :->subcmd' \
        '*: :->subcmd_args'

    case $state in
    subcmd)
        _birthday_commands
        ;;
    subcmd_args)
        case $words[2] in
        help)
            _birthday_help
            ;;
        add)
            _birthday_add
            ;;
        delete)
            _birthday_delete
            ;;
        esac
    esac
}

_birthday_commands () {
    _arguments '1: :((
            help\:"Usage of subcommands"
            show\:"Show records with filtering constrains"
            add\:"Add new record with partial informations"
            delete\:"Remove record by name"
            rename\:"Rename record by name"
        ))'
}

_birthday_help () {
    _arguments '2: :((help show add delete rename))'
}

_birthday_add () {
    local names=(${(f)"$(birthday dump names)"})
    _arguments '2: :((${names}))' \
        '3: :->comp_date'

    if [ "$state" = "comp_date" ]; then
        case "${PREFIX}" in
            */*/*|*/*/)
                for d in xx $(seq -w 31); do
                    compadd -S ' ' -p "${PREFIX%/*}/" "${d}"
                done
                ;;
            */*|*/)
                for m in xx $(seq -w 12); do
                    compadd -S '/' -p "${PREFIX%/*}/" "${m}"
                done
                compadd -S ' '  -p "${PREFIX%/*}/" "today"
                ;;
            *)
                compadd -S '/' 'xxxx' $(birthday dump years)
                ;;
        esac
    fi
}

_birthday_delete () {
    _arguments '2: :($(birthday dump names))'
}

_birthday "$@"
